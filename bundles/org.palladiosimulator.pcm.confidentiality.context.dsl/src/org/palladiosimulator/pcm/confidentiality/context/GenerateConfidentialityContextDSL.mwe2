module org.palladiosimulator.pcm.confidentiality.context.GenerateConfidentialityContextDSL

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."
var workspaceRoot = "../../"

Workflow {
	
	component = XtextGenerator {
		configuration = {
			project = StandardProjectConfig {
				baseName = "org.palladiosimulator.pcm.confidentiality.context.dsl"
				rootPath = rootPath
				runtimeTest = {
					enabled = true
				}
				eclipsePlugin = {
					enabled = true
				}
				eclipsePluginTest = {
					enabled = true
				}
				createEclipseMetaData = true
			}
			code = {
				encoding = "windows-1252"
				lineDelimiter = "\r\n"
				fileHeader = "/*\n * generated by Xtext \${version}\n */"
				preferXtendStubs = false
			}
		}
		language = StandardLanguage {
			name = "org.palladiosimulator.pcm.confidentiality.context.ConfidentialityContextDSL"
			fileExtensions = "confcontext"
			referencedResource = "platform:/resource/org.palladiosimulator.pcm.confidentiality.context/model/context.genmodel"
			
			fragment = ecore2xtext.Ecore2XtextValueConverterServiceFragment2 auto-inject {}

			formatter = {
				generateStub = true
				generateXtendStub = true
			}

			serializer = {
				generateStub = false
			}
			validator = {
				// composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
				// Generates checks for @Deprecated grammar annotations, an IssueProvider and a corresponding PropertyPage
				generateDeprecationValidation = true
			}
			generator = {
				generateXtendStub = true
			}
			junitSupport = {
				junitVersion = "5"
			}
		}
	}
	bean = org.eclipse.emf.mwe.utils.StandaloneSetup {
		platformUri = rootPath
		scanClassPath = true
		 uriMap = {
        from = "platform:/plugin/org.eclipse.emf.ecore/model/Ecore.genmodel"
        to = "platform:/resource/org.eclipse.emf.ecore/model/Ecore.genmodel"
    }
    uriMap = {
        from = "platform:/plugin/org.eclipse.emf.ecore/model/Ecore.ecore"
        to = "platform:/resource/org.eclipse.emf.ecore/model/Ecore.ecore"
    }
    uriMap = {
        from = "platform:/plugin/org.eclipse.uml2.codegen.ecore/model/GenModel.ecore"
        to = "platform:/resource/org.eclipse.uml2.codegen.ecore/model/GenModel.ecore"
    }
    uriMap = {
        from = "platform:/plugin/org.palladiosimulator.pcm/model/pcm.ecore"
        to = "platform:/resource/org.palladiosimulator.pcm/model/pcm.ecore"
    }
    uriMap = {
        from = "platform:/plugin/de.uka.ipd.sdq.identifier/model/identifier.genmodel"
        to = "platform:/resource/de.uka.ipd.sdq.identifier/model/identifier.genmodel"
    }
    uriMap = {
        from = "platform:/plugin/de.uka.ipd.sdq.identifier/model/identifier.ecore"
        to = "platform:/resource/de.uka.ipd.sdq.identifier/model/identifier.ecore"
    }
    uriMap = {
        from = "platform:/plugin/de.uka.ipd.sdq.units/model/Units.genmodel"
        to = "platform:/resource/de.uka.ipd.sdq.units/model/Units.genmodel"
    }
    uriMap = {
        from = "platform:/plugin/de.uka.ipd.sdq.units/model/Units.ecore"
        to = "platform:/resource/de.uka.ipd.sdq.units/model/Units.ecore"
    }
    uriMap = {
        from = "platform:/plugin/de.uka.ipd.sdq.stoex/model/stoex.genmodel"
        to = "platform:/resource/de.uka.ipd.sdq.stoex/model/stoex.genmodel"
    }
     uriMap = {
        from = "platform:/plugin/de.uka.ipd.sdq.stoex/model/stoex.ecore"
        to = "platform:/resource/de.uka.ipd.sdq.stoex/model/stoex.ecore"
    }
    uriMap = {
        from = "platform:/plugin/de.uka.ipd.sdq.probfunction/model/ProbabilityFunction.genmodel"
        to = "platform:/resource/de.uka.ipd.sdq.probfunction/model/ProbabilityFunction.genmodel"
    }
    uriMap = {
        from = "platform:/plugin/de.uka.ipd.sdq.probfunction/model/ProbabilityFunction.ecore"
        to = "platform:/resource/de.uka.ipd.sdq.probfunction/model/ProbabilityFunction.ecore"
    }
		
		
		
		registerGeneratedEPackage = "org.palladiosimulator.pcm.repository.RepositoryPackage"
		registerGeneratedEPackage = "org.eclipse.uml2.codegen.ecore.genmodel.GenModelPackage"
		registerGenModelFile = "platform:/resource/org.palladiosimulator.pcm/model/pcm.genmodel"
	}
}
